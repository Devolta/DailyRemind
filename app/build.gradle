apply plugin: 'com.android.application'
apply plugin: 'findbugs'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.1'

    defaultConfig {
        applicationId "com.devolta.dailyremind"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 2
        versionName "1.05"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dexOptions {
        javaMaxHeapSize "8g"
    }
}

task customFindbugs(type: FindBugs) {
    ignoreFailures = false
    effort = "max"
    reportLevel = "low"
    classes = files("$project.buildDir/intermediates/classes")

    // Use this only if you want exclude some errors
    excludeFilter = file("$rootProject.rootDir/findbugs_exclude.xml")

    source = fileTree('src/main/java/')
    classpath = files()

    reports {
        xml.enabled = false
        xml.withMessages = true
        html.enabled = !xml.isEnabled()
        xml.destination "$project.buildDir/outputs/findbugs/findbugs-output.xml"
        html.destination "$project.buildDir/outputs/findbugs/findbugs-output.html"
    }
}

build.dependsOn customFindbugs

dependencies {
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:support-v4:24.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.android.support:cardview-v7:24.1.1'
    compile 'net.xpece.android:support-preference:1.0.2'
    //compile 'com.afollestad.material-dialogs:core:0.8.6.2'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'me.grantland:autofittextview:0.2.1'
    compile('de.psdev.licensesdialog:licensesdialog:1.8.1')
    compile 'com.google.firebase:firebase-core:9.0.2'
    compile project(':recyclerview-multiselect')
    compile project(':com.devolta.devoltalibrary')
}
